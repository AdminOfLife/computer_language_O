MODULE  Text;
(*Драйвер исходного текста для компилятора JOB *)

IMPORT Location, Files, Out;

CONST
   chSpace* = " ";  (*Пробел      *)
   chTab*   = 9X;   (*Табуляция   *)
   chEOL*   = 0AX;  (*Конец строки*)
   chEOT*   = 0X;   (*Конец текста*)
   TabSize  = 3;

VAR
   Ch*    : CHAR;    (*Очередной символ      *)
   Ok*      : BOOLEAN;
   Message* : ARRAY 80 OF CHAR;

   f        : Files.File;
   r        : Files.Rider;

PROCEDURE NextCh*;
BEGIN
   Files.ReadASCII(r, Ch);    
   IF r.eof THEN
      Ch := chEOT
   ELSIF Ch=0AX THEN
      Out.Ln;
      INC(Location.Line);
      Location.Pos := 0;
      Ch := chEOL;
   ELSIF Ch = 0DX THEN
      NextCh
   ELSIF Ch # chTab THEN
      Out.Char(Ch);
      INC(Location.Pos);
   ELSE
      REPEAT
         Out.Char(" ");
         INC(Location.Pos);
      UNTIL Location.Pos MOD TabSize = 0;
   END;
END NextCh;

PROCEDURE Reset*;
BEGIN
   IF Location.Path = "" THEN
      Out.String("Формат вызова:"); Out.Ln;
      Out.String("   O <входной файл>"); Out.Ln;
      HALT(0);
   ELSE
      f := Files.Old(Location.Path);
      IF f = NIL THEN
         Ok := FALSE;
         Message :="Файл не открыт"
      ELSE
         Files.Set(r, f, 0);
         Ok := TRUE;
         Message := "Ok";
         Location.Pos := 0;
         Location.Line := 1;
         NextCh;
      END;
   END;
END Reset;

PROCEDURE Close*;
BEGIN
   Files.Close(f);
END Close;

BEGIN
   Ok := FALSE;
   Message := "Ok"
END Text.
